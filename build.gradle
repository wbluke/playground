buildscript {

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
    }

}

allprojects {

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

}

def javaProjects = [
        project(':playground-batch'),
        project(':playground-core'),
        project(':playground-learning-test'),
        project(':playground-logging-system'),
        project(':playground-web'),
]

configure(javaProjects) {

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.wbluke.playground'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = 1.8

    dependencies {
        implementation 'org.projectlombok:lombok'
        testImplementation 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        runtimeOnly 'com.h2database:h2'

        implementation 'org.springframework.boot:spring-boot-starter-web'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }
    }

    test {
        useJUnitPlatform()
    }

    /* 해당 task로 서브 모듈 추가시 기초적인 디렉토리를 자동으로 생성 */
    task initSourceFolders {
        sourceSets*.java.srcDirs*.each {
            if (!it.exists()) {
                it.mkdirs()
            }
        }

        sourceSets*.resources.srcDirs*.each {
            if (!it.exists()) {
                it.mkdirs()
            }
        }
    }

}
